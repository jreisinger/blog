#!/usr/bin/perl
# Show posts (posts/*.md) that are not listed in README.md or have no symlink
# within subdirectories.
# Usage: blog$ ./bin/wild-posts
use strict;
use warnings;
use 5.010;
use autodie;
use File::Find;
use File::Basename;

use lib 'lib';
use Local::Display;

my @posts;
find( sub { push @posts, $File::Find::name if -f && ! -l && /\.md$/ }, 'posts' );

my @readme_links;
open my $fh, "<", "README.md";
while (<$fh>) {
    push @readme_links, $1 if /\((posts\/[^\)]+)\)/;
}

for my $post ( sort @posts ) {

    if ( -l $post ) {

        # convert symlink to basename of its value
        $post = basename( readlink $post );
    } else {
        warn "WARN: $post is not symlinked within any subdirectory\n"
          unless symlink_exists($post);
    }

    my $post_in_readme = grep $post eq $_, @readme_links;

    my $readme_line = make_readme_line($post);

    if (not $post_in_readme) {
        say "Not in README:";
        say $readme_line if $readme_line;
    } 
}

# Is there at least one symlink, i.e. category, for this post?
sub symlink_exists {
    my $post = shift;

    my @symlinks;
    find( sub { push @symlinks, $File::Find::name if -l && /\.md$/ }, 'posts' );

    for my $link (@symlinks) {
        return 1 if basename($link) eq basename($post);
    }

    return;
}
